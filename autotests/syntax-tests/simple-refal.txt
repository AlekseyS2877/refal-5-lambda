non-terminal Program, Global, ItemList, Function, Body, Sentence, PatternEx,
  Pattern, Result, PatternTerm, Atom, ResultTerm, Keyword, Transform;
terminal 'Name', ',', ';',
  '$EXTERN', '$ENUM', '$EENUM', '$SWAP', '$ESWAP', '$FORWARD', '$LABEL',
  '$ENTRY', '{', '}', '(', ')', '[', ']', 'e.Var', '^', '#',
  ''chars'', '123', '<', '>', '=', ':';

Program ::= Global*;
Global ::= ItemList | Function | ';';
ItemList ::= Keyword 'Name' (',' 'Name')* ';';
Keyword ::= '$EXTERN' | '$ENUM' | '$EENUM' | '$SWAP' | '$ESWAP' | '$FORWARD'
  | '$LABEL';
Function ::= ('$ENTRY')? 'Name' Body;
Body ::= '{' Sentence* '}';
Sentence ::= PatternEx '=' Result (':' Transform)* ';' ;
Transform ::= PatternEx '=' Result | Body;
PatternEx ::= Pattern (',' Result ':' (Body ':')* Pattern)*;
Pattern ::= PatternTerm*;
PatternTerm ::= Atom | '(' Pattern ')' | '[' 'Name' Pattern ']' | 'e.Var' ('^')?;
Atom ::= ('#')? 'Name' | ''chars'' | '123';
Result ::= ResultTerm*;
ResultTerm ::= Atom | '(' Result ')' | '[' 'Name' Result ']' | 'e.Var' | Body
  | '<' Result '>';
