<?xml version="1.0" encoding="UTF-8" standalone="yes" ?>
<job id="installer">
  <script language="VBScript" src="constants.vbs"/>
  <script language="VBScript">
<![CDATA[
Option Explicit
Dim WshShell, WshEnvUser, WshNamedArgs
Dim InstallDir, FSO, BinDir, DecoratedBinDir

Const CUninstallKey = _
  "HKCU\Software\Microsoft\Windows\CurrentVersion\Uninstall\Refal-5-lambda"

Private Function Decorate(Text)
  Decorate = ";" & Text & ";"
End Function

Private Sub Init
  Set WshShell = CreateObject("WScript.Shell")
  Set WshEnvUser = WshShell.Environment("USER")
  Set WshNamedArgs = WScript.Arguments.Named
  Set FSO = CreateObject("Scripting.FileSystemObject")

  InstallDir = FSO.GetParentFolderName(WScript.ScriptFullName)
  BinDir = InstallDir & "\bin"

  DecoratedBinDir = Decorate(BinDir)
End Sub

Private Sub AddBinDirToPath(Env)
  Dim Path, DecoratedPath
  Path = WshEnvUser(Env)
  DecoratedPath = Decorate(Path)

  If Not InStr(1, DecoratedPath, DecoratedBinDir, vbTextCompare) > 0 Then
    WshEnvUser(Env) = Path & ";" & BinDir
  End If
End Sub

Private Sub Install
  AddBinDirToPath "PATH"

  WshShell.RegWrite CUninstallKey & "\DisplayIcon", _
    InstallDir & "\icon.ico", "REG_SZ"
  WshShell.RegWrite CUninstallKey & "\DisplayName", _
    "Refal-5-lambda", "REG_SZ"
  WshShell.RegWrite CUninstallKey & "\DisplayVersion", CVersion, "REG_SZ"
  WshShell.RegWrite CUninstallKey & "\EstimatedSize", _
    CByteSize \ 1024, "REG_DWORD"
  WshShell.RegWrite CUninstallKey & "\Publisher", "BMSTU IU-9", "REG_SZ"
  WshShell.RegWrite CUninstallKey & "\UninstallString", _
    """%SystemRoot%\system32\wscript.exe"" " & _
    """" & InstallDir & "\uninstall.wsf""", "REG_EXPAND_SZ"

  WScript.Echo("Successfully installed")
End Sub

Private Function UnDecorate(Text)
  Text = Left(Text, Len(Text) - 1)      'Skip right ";"
  Text = Right(Text, Len(Text) - 1)     'Skip left ";"
  UnDecorate = Text
End Function

Private Sub RemoveBinDirFromPath(Env)
  Dim Path
  Path = WshEnvUser(Env)
  Path = Decorate(Path)
  Path = Replace(Path, DecoratedBinDir, ";", 1, -1, vbTextCompare)
  Path = UnDecorate(Path)
  WshEnvUser(Env) = Path
End Sub

Private Sub UnInstall
  RemoveBinDirFromPath "PATH"

  WshShell.CurrentDirectory = InstallDir & "\.."
  FSO.DeleteFolder InstallDir, True
  FSO.DeleteFolder WshShell.SpecialFolders.Item("Programs") & "\Refal-5-lambda", True

  WshShell.RegDelete CUninstallKey & "\"

  WScript.Echo("Successfully uninstalled")
End Sub


Init

If WshNamedArgs.Exists("install") Then
  Install
Else
  UnInstall
End If
]]>
  </script>
</job>
