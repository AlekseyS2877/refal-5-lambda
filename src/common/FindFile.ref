*$FROM LibraryEx
$EXTERN Map;


/**
  <FindFiles s.TargetMode (e.Folders) e.Files>
    == t.FoundFile*

  s.TargetMode ::=
      DefaultEXE   -- subdir 'exe'
    | EXE          -- subdir 'exe'
    | LIB          -- subdir 'lib'
    | R            -- subdir 'lib'
    | CompileOnly  -- subdir 'exe'
  t.FoundFile ::=
    (Source (s.Dialect e.Source) e.Output)
    (Output e.Output)
    (OutputWithNative (e.Output) e.Native)
    (NotFound e.FileName)

  s.Dialect ::= R5 | SR
*/
$ENTRY FindFiles {
  s.TargetMode (e.Folders) e.Files
    = s.TargetMode
    : {
        DefaultEXE = 'exe';
        EXE = 'exe';
        LIB = 'lib';
        R = 'lib';
        CompileOnly = 'exe';
      }
    : e.Subdir
    = <Map
        {
          (e.FileName)
            = <AnalyzeFile-CheckNotFound
                (e.FileName)
                <AnalyzeFile e.Subdir '/' e.FileName>
                <AnalyzeFile e.FileName>
                <Map
                  {
                    (e.Folder)
                      = <AnalyzeFile e.Folder '/' e.Subdir '/' e.FileName>
                        <AnalyzeFile e.Folder '/' e.FileName>;
                  }
                  e.Folders
                >
              >;
        }
        e.Files
      >;
}

AnalyzeFile-CheckNotFound {
  (e.FileName) (Source (s.Dialect e.Source) e.Output) e.Variants =
    (Source (s.Dialect e.Source) e.Output);

  (e.FileName) (Output e.Output) e.Variants =
    (Output e.Output);

  (e.FileName) (OutputWithNative (e.Output) e.Native) e.Variants =
    (OutputWithNative (e.Output) e.Native);

  (e.FileName) (NotFound e.NotFoundPath) e.Variants =
    <AnalyzeFile-CheckNotFound (e.FileName) e.Variants>;

  (e.FileName) = (NotFound e.FileName);
}

ExistFile-T {
  e.FileName = <ExistFile e.FileName> e.FileName;
}

AnalyzeFile {
  e.FileName '.ref'
    = <ExistFile-T e.FileName '.ref'>
    : {
        True e.UnitName '.ref' =
          (Source (R5 e.UnitName '.ref') e.UnitName '.rasl');

        False e.SourceName = (NotFound e.SourceName);
      };

  e.FileName '.sref'
    = <ExistFile-T e.FileName '.sref'>
    : {
        True e.UnitName '.sref' =
          (Source (SR e.UnitName '.sref') e.UnitName '.rasl');

        False e.SourceName = (NotFound e.SourceName);
      };

  e.FileName '.rasl'
    = <ExistFile-T e.FileName '.rasl'>
    : {
        True e.OutName
          = <ExistFile-T e.FileName '.cpp'>
          : {
              True e.NativeName = (OutputWithNative (e.OutName) e.NativeName);

              False e.NativeName = (Output e.OutName);
            };

        False e.OutName = (NotFound e.OutName);
      };

  e.FileName
    = (<ExistFile-T e.FileName '.ref'>) (<ExistFile-T e.FileName '.sref'>)
      (<ExistFile-T e.FileName '.rasl'>) (<ExistFile-T e.FileName '.cpp'>)
    : {
        (True e.R5-SourceName) (s.ResSR e.SR-SourceName)
        (s.ResOut e.OutName) (s.ResNat e.Native)
          = (Source (R5 e.R5-SourceName) e.OutName);

        (False e.R5-SourceName) (True e.SR-SourceName)
        (s.ResOut e.OutName) (s.ResNat e.Native)
          = (Source (SR e.SR-SourceName) e.OutName);

        (False e.R5-SourceName) (False e.SR-SourceName)
        (True e.OutName) (True e.Native)
          = (OutputWithNative (e.OutName) e.Native);

        (False e.R5-SourceName) (False e.SR-SourceName)
        (True e.OutName) (False e.Native)
          = (Output e.OutName);

        (False e.UnitName '.ref') (False e.UnitName '.sref')
        (False e.UnitName '.rasl') (False e.UnitName '.cpp')
          = (NotFound e.UnitName);
      };
}

/**
  <FindInclude (e.Folders) e.FileName>
    == Success s.Dialect e.FullPath
    == Fails
  s.Dialect ::= R5 | SR
*/
$ENTRY FindInclude {
  (e.Folders) e.FileName
    = <AnalyzeInclude e.FileName>
      <Map
        {
          (e.Folder) = <AnalyzeInclude e.Folder '/' e.FileName>;
        }
        e.Folders
      >
    : {
        e.Variants-B (Success s.Dialect e.FullPath) e.Variants-E
          = Success s.Dialect e.FullPath;

        e.Variants = Fails;
      };
}

AnalyzeInclude {
  e.FileName '.refi'
    = <ExistFile-T e.FileName '.refi'>
    : {
        True e.FullPath = (Success R5 e.FullPath);
        False e.FullPath = (Fails);
      };

  e.FileName '.srefi'
    = <ExistFile-T e.FileName '.srefi'>
    : {
        True e.FullPath = (Success SR e.FullPath);
        False e.FullPath = (Fails);
      };

  e.FileName
    = (<ExistFile-T e.FileName '.refi'>) (<ExistFile-T e.FileName '.srefi'>)
    : {
        (True e.R5-FullPath) (s.ResSR e.SR-FullPath)
          = (Success R5 e.R5-FullPath);

        (False e.R5-FullPath) (True e.SR-FullPath)
          = (Success SR e.SR-FullPath);

        (False e.R5-FullPath) (False e.SR-FullPath) = (Fails);
      };
}
