$INCLUDE "LibraryEx";


*FROM Desugaring-UnCondition
$EXTERN ExtractVariables;

*FROM GenericMatch
$EXTERN Solve;

/**
  <OptTree-Drive-ExtractOptInfo s.OptDrive e.AST>
    == (e.DriveInfo) e.AST

  s.OptDrive ::= OptDrive | OptInline | NoOpt
  e.DriveInfo ::= (e.DriveFuncNames) e.DriveInfo-Specific
  e.DriveFuncNames ::= (e.FuncName)*
*/
$ENTRY OptTree-Drive-ExtractOptInfo {
  s.OptDrive e.AST
    = <GetDrivenFunctions e.AST>
    : ((e.Names) e.Info) e.AST^
    = s.OptDrive
    : {
        NoOpt = ((/* нет имён */) /* нет инфы */) e.AST;

        s.OptDrive^ = ((e.Names) e.Info) e.AST;
      }
}

/**
  <OptTree-Drive s.OptDrive (e.DriveInfo) e.AST> == (e.DriveInfo^) e.AST^
*/
$ENTRY OptTree-Drive {
  NoOpt (e.DriveInfo) e.AST = (e.DriveInfo) e.AST ;
  s.Opt (e.DriveInfo) e.AST
    = <DriveInlineOptimizerTick (e.DriveInfo) e.AST s.Opt>;
}

/**
  <OptTree-Drive-Finalize s.OptDrive (e.DriveInfo) e.AST> == e.AST^
*/
$ENTRY OptTree-Drive-Finalize {
  s.OptDrive ((e.Names) e.Info) e.AST = e.AST;
}

/* Функция генерирует имена остаточных прогоняемых функций */

GenRemainderFuncNames {
    (e.Name) (e.Sentences) =
      <MapAccum
       {
          ((e.NewName SUF e.Suffix) (e.Names) s.N) t.A =
            ((e.Name) (e.Names (e.Name '*' s.N)) <Add s.N 1>);

          ((e.NewName) (e.Names) s.N) t.A =
            ((e.Name)(e.Names (e.Name SUF '*' s.N)) <Add s.N 1>);
       }
       ((e.Name) () 1)
       e.Sentences
      > : ((e.Name^) (e.Names) t.N)
       = e.Names
}

GetDrivenFunctions {
  e.AST

    /* Выносим все Drive и Inline из дерева */
    = <MapAccum
        {
          (e.Optimizable) (Drive e.Name) = (e.Optimizable (Drive e.Name));
          (e.Optimizable) (Inline e.Name) = (e.Optimizable (Inline e.Name));
          (e.Optimizable) t.Other = (e.Optimizable) t.Other;
        }
        () e.AST
      >
    : (e.OptNames) e.AST^

    /*
      Выделяем тела оптимизируемых функций,
      копировать несколько имён в e.OptimizableNames не страшно

      Используем MapAccum не смотря на то, что достаточно Map:
      избегаем копирования дерева
    */
    = <MapAccum
        {
          ((e.OptFunctions) (e.RemNames))
          (Function s.ScopeClass (e.Name) e.Body)
            , e.OptNames : e.B (s.Label e.Name) e.E
            , e.Body : Sentences e.Sentences
            = ((e.OptFunctions (s.Label s.ScopeClass (e.Name) { = e.Body}))
              (e.RemNames <GenRemainderFuncNames (e.Name) (e.Sentences)>))
              (Function s.ScopeClass (e.Name) e.Body);

          (e.OptInfo) t.Other
            = (e.OptInfo) t.Other;
        }
        (() ()) e.AST
      >
    : ((e.OptFunctions) (e.RemNames)) e.AST^

    = ((<Map { (s.Label e.Name ) = (e.Name) } e.OptNames> e.RemNames)
       e.OptFunctions)
       e.AST;
}

OptSwitch {
  OptDrive = Drive;
  OptInline = Inline;
}

/* Осуществляет прогонку или оптимизацию в зависимости от ключа */

DriveInlineOptimizerTick {
  ((e.OptNames) e.OptFunctions) e.AST s.Opt
    = <OptSwitch s.Opt> : s.Mode
    = ((e.OptNames) e.OptFunctions)
      <Map
        {
          (Function s.ScopeClass (e.Name) e.Body)
            = <OptimizeF
                (Function s.ScopeClass (e.Name) e.Body)
                ((e.OptNames) e.OptFunctions)
                s.Mode
              >;

          e.Other = e.Other
        }
        e.AST
      >: e.AST^
    = <DistinctFuncs e.AST>
}

DistinctFuncs {
  e.B
  (Function s.ScopeClass (e.Name) e.Body1)
  e.M
  (Function s.ScopeClass (e.Name) e.Body2)
  e.E
  = <DistinctFuncs
      e.B (Function s.ScopeClass (e.Name) e.Body1) e.M e.E
    >;

  e.Other = e.Other
}

IsPassiveCall {
  e.Expr
    = <Reduce
        {
           True t.Term = <IsPassiveCall-Aux t.Term>;
           False t.Term = False;
        }
        True
        e.Expr
      >

}

IsPassiveCall-Aux {
  (Brackets e.Expr) = <IsPassiveCall e.Expr>;
  (ADT-Brackets e.Expr) = <IsPassiveCall e.Expr>;

  (CallBrackets e.Expr) = False;
  (ColdCallBrackets e.Expr) = False;

  t.Other = True;
}

IsLexpr {
  e.Expr
    , <IsLexpr-Aux e.Expr>
    : {
        e.Res-B OpenE e.Res-E = False;
        e.Res-B (s.Mode e.Index) e.Res-M (s.Mode e.Index) e.Res-E = False;
        e.Other = True;
      }
}

/*
  <IsLexpr-Aux e.Expr> == t.IsLexpr-Aux-Item*

  t.IsLexpr-Aux-Item ::= OpenE | ('e' e.Index) | ('t' e.Index)
*/
IsLexpr-Aux {
  e.Expr-B (TkVariable 'e' e.Index1) e.Expr-M (TkVariable 'e' e.Index2) e.Expr-E
    = OpenE;

  e.Expr
    = <Map
        {
          (TkVariable 'e' e.Index) = ('e' e.Index);
          (TkVariable 't' e.Index) = ('t' e.Index);

          (Brackets e.InBrackets) = <IsLexpr-Aux e.InBrackets>;
          (ADT-Brackets (e.Name) e.InBrackets) = <IsLexpr-Aux e.InBrackets>;

          t.Other = /* пропускаем */;
        }
        e.Expr
      >;
}

/*
  Осуществляет попытку прогонки или встраивания в функции
  <OptimizeF t.Function (t.DriveInfo) s.Mode> == t.Function*

  Возвращает модифицированную функцию
  и порожденные из прогонки или встраивания
*/

OptimizeF {
  (Function s.ScopeClass (e.Name) Sentences e.Sentences)
  ((e.OptNames) e.OptFunctions) s.Mode
    = <Reduce
        {
          ((e.SntAcc) (e.FunAcc)) t.Sentence
            = <OptSentence
                t.Sentence
                ((e.OptNames) e.OptFunctions)
                s.Mode
              >
            : (e.NewFunctions) e.NewSentences
            = ((e.SntAcc e.NewSentences) (e.FunAcc e.NewFunctions));
        }
        ((/*sentences*/) (/*functions*/))
        e.Sentences
      > : ((e.Sentences^) (e.Functions))
    = (Function s.ScopeClass (e.Name) Sentences e.Sentences) e.Functions;

  e.Other = e.Other;
}

/*
   <OptSentence t.Sentence t.DriveInfo s.Mode>
   == (t.Func?) t.Sentence*
*/

OptSentence {
  ((e.Left) (e.Right)) t.DriveInfo s.Mode
     , <FindOptimizedCall t.DriveInfo e.Right> : t.Res e.Right^
     , <OneOf t.Res None>: False
     = <OptSentence-Aux s.Mode ((e.Left) (e.Right)) t.Res t.DriveInfo>;

  t.Sentence ((e.OptNames) e.Left) s.Mode = () t.Sentence
}

OptSentence-Aux {
  s.Mode
  ((e.Left) (e.Right))
  (CallBrackets (Symbol Name e.Name) e.Args)
  t.DriveInfo

    /* Поиск информации для прогонки */
    = <FindOptInfo t.DriveInfo e.Name> : Success t.FunctionForDrive

    = <OptSentence-MakeSubstitutions
        s.Mode ((e.Left) (e.Right)) (e.Args) t.FunctionForDrive
      >
    : e.SolutionPacks
    = <MapAccum
        {
          (e.NewFunctions) ((e.RS^) e.Solutions^ (e.NewFunction))
            = (e.NewFunctions e.NewFunction)
              <ApplySolutions ((e.Left) (e.Right)) (e.RS) e.Solutions>;
        }
        (/* new functions */)
        e.SolutionPacks
      >;
}

OptSentence-MakeSubstitutions {
  s.Mode
  ((e.Left) (e.Expr)) (e.Args)
  (s.FuncMode s.ScopeClass (e.Name) Sentences e.Body)

    /* Активные вызовы игнорируем */
    , <IsPassiveCall e.Args> : False

    = (
        ((ColdCallBrackets (Symbol Name e.Name) e.Args))
        ((/* нет присваиваний */) (/* нет сужений */))
        (/* нет новых функций */)
      );

  s.Mode
  ((e.Left) (e.Expr)) (e.Args)
  (s.FuncMode s.ScopeClass (e.Name) Sentences ((e.LS) (e.RS)) e.Rest)

    /* Если левая часть не L-выражение, допускаем только встраивание */
    = <IsLexpr e.Left>
    : {
        True = s.Mode;
        False = Inline;
      }
    : s.Mode^

    = <Solve
        (<ExtractVariables ((e.Left) (e.Expr))>)
        ((e.Args) ':' (e.LS))
      >
    : e.Solution

    = <DecreaseMode s.Mode s.FuncMode> : s.Mode^

    = <OptSentence-MakeSubstitutions-Aux
        (e.RS) s.Mode (e.Name) (e.Rest) (e.Args)
        e.Solution
      >;

  s.Mode
  ((e.Left) (e.Expr)) (e.Args)
  (s.FuncMode s.ScopeClass (e.Name) Sentences e.Body)
    = (
        ((ColdCallBrackets (Symbol Name e.Name) e.Args))
        ((/* нет присваиваний */) (/* нет сужений */))
        (/* нет новых функций */)
      );
}

DecreaseMode {
  Inline s.Mode = Inline;
  Drive s.Mode = s.Mode;
}

OptSentence-MakeSubstitutions-Aux {
  /*
    В случае решения без сужений просто применяем замены
  */
  (e.RS) s.Mode (e.Name) (e.Rest) (e.Args)
  Success (() (e.Assigns))
    = ((e.RS) (() (e.Assigns)) (/* нет новых функций */));

  /*
     В случае "грязного" решения добавляем вызов остаточной
     прогоняемой функции (Только при L-выражении)
  */
  (e.RS) s.Mode (e.Name) (e.Rest) (e.Args)
  Success e.M
    , s.Mode : Drive
    = <CreateRemainderDrivenFunc (e.Name) e.Rest>
    : t.newFunc
    , t.newFunc : (Function s.Sc1 (e.NewName) e.Body)
    = ((e.RS) e.M (/* нет новых функций */))
      (
        ((CallBrackets (Symbol Name e.NewName) e.Args))
        ((/* нет присваиваний */) (/* нет сужений */))
        (t.newFunc)
      );

  /*
    В случае неудачи нужно заменить прогоняемый вызов
    на вызов остаточной функции
  */
  (e.RS) s.Mode (e.Name) (e.Rest) (e.Args)
  Failure
    = <CreateRemainderDrivenFunc (e.Name) e.Rest>
    : t.newFunc
    , t.newFunc : (Function s.Sc1 (e.NewName) e.Body)
    = (
        ((CallBrackets (Symbol Name e.NewName) e.Args))
        ((/* нет присваиваний */) (/* нет сужений */))
        (t.newFunc)
      );

  /*
    Замена вызова на холодный

    Undefined или левая часть не-L-выражение
  */
  (e.RS) s.Mode (e.Name) (e.Rest) (e.Args)
  e.Other
    = (
        ((ColdCallBrackets (Symbol Name e.Name) e.Args))
        ((/* нет присваиваний */) (/* нет сужений */))
        (/* нет новых функций */)
      );
}

DropLeft {
  e.List 0 =  e.List;
  t.Head e.Tail s.N = <DropLeft e.Tail <Sub s.N 1>>
}

/*
  <FindOptInfo t.DriveInfo e.Name> == Success t.Function | Failure
  t.Function := (Drive s.ScopeClass (e.Name) e.Body)
*/

FindOptInfo {
  t.DriveInfo e.Name
    , e.Name : e.Prefix '*' s.N
    , <CutSUF e.Prefix> : e.Prefix^
    , t.DriveInfo
    : (
        (e.B1 (e.Prefix) e.E1)
        e.B2 (s.Mode s.ScopeClass (e.Prefix) s.Body) e.E2
      )
    = <s.Body> : Sentences e.Sentences
    = Success
      (s.Mode s.ScopeClass (e.Name) Sentences <DropLeft e.Sentences s.N>);

  t.DriveInfo e.Name
    , t.DriveInfo
    : (
        (e.B1 (e.Name) e.E1)
        e.B2 (s.Mode s.ScopeClass (e.Name) s.Body) e.E2
      )
    = Success (s.Mode s.ScopeClass (e.Name) <s.Body>);

  t.DriveInfo e.Name = Failure;
}

$DRIVE CutSUF;

CutSUF {
  e.Prefix SUF = e.Prefix;
  e.Prefix = e.Prefix;
}

/*
  <FindOptimizedCall t.InlineInfo e.Expr> == t.Found e.Expr

  t.Found ::= None | (CallBrackets e.Arg)
*/

FindOptimizedCall {
  ((e.OptFuncNames) e.OptFuncBodies) e.Expr
    = <FindOptimizedCall-Aux (e.OptFuncNames) e.Expr>
    : (e.OptFuncNames^ t.Found) e.Expr^
    = t.Found e.Expr
}

FindOptimizedCall-Aux {
  (e.OptFuncNames) e.Expr
    = <MapAccum &FindOptimizedCall-Term (e.OptFuncNames None) e.Expr>;
}

FindOptimizedCall-Term {
  (e.OptFuncNames None) (Brackets e.Expr)
    = <FindOptimizedCall-Aux (e.OptFuncNames) e.Expr>
    : (e.OptFuncNames^ t.Found) e.Expr^
    = (e.OptFuncNames t.Found) (Brackets e.Expr);

  (e.OptFuncNames None) (ADT-Brackets (e.Name) e.Expr)
    = <FindOptimizedCall-Aux (e.OptFuncNames) e.Expr>
    : (e.OptFuncNames^ t.Found) e.Expr^
    = (e.OptFuncNames t.Found)
      (ADT-Brackets (e.Name) e.Expr);

  (e.OptFuncNames-B (e.Name) e.OptFuncNames-E None)
  (CallBrackets (Symbol Name e.Name) e.Expr)
    , <IsPassiveCall e.Expr> : True
    = (
        e.OptFuncNames-B (e.Name) e.OptFuncNames-E
        (CallBrackets (Symbol Name e.Name) e.Expr)
      )
      (TkVariable 'e' DRIVEN 0);

* TODO: раскомментировать код ниже, когда будет реализовываться поддрежка
* TODO: активных аргументов функций
/*
  (e.OptFuncNames-B (e.Name) e.OptFuncNames-E None)
  (CallBrackets (Symbol Name e.Name) e.Expr)
    , <FindOptInfo t.DriveInfo e.Name> : Success t.Info
    = e.OptFuncNames-B (e.Name) e.OptFuncNames-E : e.OptFuncNames
    = <FindOptimizedCall-Aux (e.OptFuncNames) e.Expr>
    : {
        (e.OptFuncNames^ None) e.Expr^
          = (e.OptFuncNames (CallBrackets (Symbol Name e.Name) e.Expr))
            (TkVariable 'e' DRIVEN 0);

        (e.OptFuncNames^ t.Found) e.Expr^
          = (e.OptFuncNames t.Found) (CallBrackets (Symbol Name e.Name) e.Expr);
      };
*/

  (e.OptFuncNames None) (CallBrackets t.F e.Expr)
    = <FindOptimizedCall-Aux (e.OptFuncNames) e.Expr>
    : (e.OptFuncNames^ t.Found) e.Expr^
    = (e.OptFuncNames t.Found) (CallBrackets t.F e.Expr);

  (e.OptFuncNames t.Found) t.AnyTerm
    = (e.OptFuncNames t.Found) t.AnyTerm;
}

/*
  <ApplySolutions
    ((e.Left) (e.Right)) (e.LS) (e.Solutions)
  > == t.Sentence* t.FunctionInfo
*/

ApplySolutions {

  ((e.Left) (e.Right)) (e.RS) e.Solutions

  = <Map
      {
        t.S
          = <ApplySolution
             ((e.Left) (e.Right)) (e.RS) t.S
            >
      }
      e.Solutions
    >
}

/*
  <ApplySolution
    ((e.Left) (e.Right)) (e.LS) (e.Contrs) (e.Assigns)
  > == t.Sentence
*/

ApplySolution {
  ((e.Left) (e.Right)) (e.RS) ((e.Contrs) (e.Assigns))
    = <ApplyAssigns e.RS (e.Assigns)>: e.CallReplacer
    = e.Contrs ((TkVariable 'e' DRIVEN 0) ':' e.CallReplacer): e.Contrs^
    = ((<ApplyContractions e.Left (e.Contrs)>)
       (<ApplyContractions e.Right (e.Contrs)>));
}

/*
  <ApplyAssigns (e.Expr) (e.Assigns)> == e.Expr^
*/

ApplyAssigns {
  e.Expr (e.Assigns)
    = <Map
        {
          t.T = <ApplyAssigns-Term t.T (e.Assigns)>
        }
        e.Expr
      >
}

ApplyAssigns-Term {
  (Brackets e.Expr) (e.Assigns) = (Brackets <ApplyAssigns e.Expr (e.Assigns)>);

  (CallBrackets e.Args) (e.Assigns)
    = (CallBrackets <ApplyAssigns e.Args (e.Assigns)>);

  (ColdCallBrackets e.Args) (e.Assigns)
    = (ColdCallBrackets <ApplyAssigns e.Args (e.Assigns)>);

  (ClosureBrackets e.Args) (e.Assigns)
    = (ClosureBrackets <ApplyAssigns e.Args (e.Assigns)>);

  (ADT-Brackets (e.Name) e.Expr) (e.Assigns)
    = (ADT-Brackets (e.Name) <ApplyAssigns e.Expr (e.Assigns)>);

  t.T (e.B (e.Asgn ':' t.T) e.E)
    =  e.Asgn;

  t.T (e.Assigns) = t.T;
}

/*
  <ApplyContractions (e.Expr) (e.Contrs) == e.Expr^
*/
ApplyContractions {
  e.Expr (e.Contrs)
    = <Map
        {
          t.T = <ApplyContractions-Term t.T (e.Contrs)>
        }
        e.Expr
      >;
}

ApplyContractions-Term {
  (Brackets e.Expr) (e.Contrs) = (Brackets <ApplyContractions e.Expr (e.Contrs)>);

  (CallBrackets e.Args) (e.Contrs)
    = (CallBrackets <ApplyContractions e.Args (e.Contrs)>);

  (ColdCallBrackets e.OriginArgs) (e.Contrs)
    = <ApplyContractions e.OriginArgs (e.Contrs)>
    : {
*        Содержимое холодных скобок не изменилось, оставляем их холодными
        e.OriginArgs = (ColdCallBrackets e.OriginArgs);

*       Содержимое обновилось, оттаиваем скобки
        e.ChangedArgs = (CallBrackets e.ChangedArgs);
      };

  (ClosureBrackets e.Args) (e.Contrs)
    = (ClosureBrackets <ApplyContractions e.Args (e.Contrs)>);

  (ADT-Brackets (e.Name) e.Expr) (e.Contrs)
    = (ADT-Brackets (e.Name) <ApplyContractions e.Expr (e.Contrs)>);

  t.Term (e.B (t.Term ':' e.Contr) e.E)
    = <ApplyContractions e.Contr (e.B (t.Term ':' e.Contr) e.E)>;

  t.T (e.Contrs) = t.T
}

/*
  <CreateRemainderDrivenFunc (e.Name) e.Sentences>
    == t.Function
*/

CreateRemainderDrivenFunc {
  (e.Name SUF e.Suffixes '*' s.N) e.Sentences =
    (Function GN-Local (e.Name SUF e.Suffixes '*' <Add s.N 1>) Sentences e.Sentences);

  (e.Name SUF e.Suffixes) e.Sentences =
    (Function GN-Local (e.Name SUF e.Suffixes '*' 1) Sentences e.Sentences);

  (e.Name) e.Sentences =
    (Function GN-Local (e.Name SUF '*' 1) Sentences e.Sentences)
}

