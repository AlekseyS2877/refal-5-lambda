$INCLUDE "LibraryEx";


/**
  Внутренний формат:
  [ErrorList (e.FileName) (t.SrcPos e.Message)*]
  t.SrcPos ::=
      NoPos
    | (FileLine s.LineNumber e.FileName)
    | (RowCol s.Row s.Col)
    | (FileRowCol (s.Row s.Col) e.FileName)
*/
$ENUM ErrorList;

$ENTRY EL-Create {
  e.FileName = [ErrorList (e.FileName)];
}

$ENTRY EL-AddError {
  [ErrorList (e.FileName) e.Errors] e.Message =
    [ErrorList (e.FileName) e.Errors (NoPos e.Message)];
}

$ENTRY EL-AddErrorAt {
  [ErrorList (e.FileName) e.Errors] NoPos e.Message =
    [ErrorList
      (e.FileName) e.Errors (NoPos e.Message)
    ];

  [ErrorList (e.FileName) e.Errors] s.LineNumber e.Message =
    [ErrorList
      (e.FileName) e.Errors ((FileLine s.LineNumber e.FileName) e.Message)
    ];

  [ErrorList (e.FileName) e.Errors] (RowCol s.Row s.Col) e.Message
    = [ErrorList
        (e.FileName) e.Errors ((FileRowCol (s.Row s.Col) e.FileName) e.Message)
      ];

  [ErrorList (e.FileName) e.Errors] t.SrcPos e.Message =
    [ErrorList (e.FileName) e.Errors (t.SrcPos e.Message)];
}

$ENTRY EL-Concat {
  [ErrorList (e.FileName-L) e.Errors-L] [ErrorList (e.FileName-R) e.Errors-R]
    = [ErrorList (e.FileName-L) e.Errors-L e.Errors-R];
}

$ENTRY PrintErr {
  e.Message = <Putout stderr e.Message>;
}

$ENTRY EL-Destroy {
  [ErrorList (e.FileName)] = EL-NoErrors;

  [ErrorList (e.FileName) e.Errors] =
    <Map
      {
        (NoPos e.Message) =
          <PrintErr '  ERROR: ' e.Message>;

        ((FileLine s.LineNumber e.FileName^) e.Message) =
          <PrintErr
            e.FileName ':' <Symb s.LineNumber> ':ERROR: ' e.Message
          >;

        ((FileRowCol (s.Row s.Col) e.FileName^) e.Message)
          = <PrintErr
              e.FileName ':' <Symb s.Row> ':' <Symb s.Col>
              ':' e.Message
            >;
      }
      <Sort e.Errors>
    >
    EL-HasErrors;
}
