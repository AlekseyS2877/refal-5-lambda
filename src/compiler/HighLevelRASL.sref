//FROM LibraryEx
$EXTERN Map;

//FROM HighLevelRASL-DisjointFunc
$EXTERN HighLevelRASL-Function-Disjoint;

//FROM HighLevelRASL-ConjointFunc
$EXTERN HighLevelRASL-Function-Conjoint;

//FROM HighLevelRASL-GenSubst-Simple
$EXTERN GenInitSubst-Simple, GenSubst-Simple;

//FROM HighLevelRASL-GenResult-Simple
$EXTERN GenResult-Simple;

//FROM HighLevelRASL-GenSubst-Save
$EXTERN GenInitSubst-Save, GenSubst-Save;

//FROM HighLevelRASL-GenResult-Opt
$EXTERN GenResult-Opt;

/**
  <HighLevelRASL s.Joint s.OptResult e.AST>
    == e.RASLAST

  e.RASLAST ::= t.RASLFunction*
  t.RASLFunction ::=
      (#Function s.ScopeClass (e.Name) t.HiRASLCommand*)
    | (#CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code)
    | (s.SingularItem s.ScopeClass e.Name)
    | (#CmdDefineIdent e.Name)
    | (#CmdEmitNativeCode t.SrcPos e.Code)
  s.SingularItem ::= #CmdEnum | #CmdSwap | #CmdDeclaration
  t.HiRASLCommand ::=
      (#Cmd… e.Value)
    | (#CmdSentence t.HiRASLCommand*)
    | (#CmdOpenELoop #AlgLeft s.BracketNum s.VarNumber t.HiRASLCommand*)
*/
$ENTRY HighLevelRASL {
  s.Joint s.OptResult e.ProgramElements =
    <Map
      {
        (#Function s.ScopeClass (e.Name) #Sentences e.Sentences) =
          <Map
            {
              ((e.Pattern) e.Conditions (e.Result))
                = <Map
                    {
                      (#Condition (e.Name^) (e.Result^) (e.Pattern^))
                        = (#CmdEnum #GN-Local e.Name);
                    }
                    e.Conditions
                  >;
            }
            e.Sentences
          >
          <HighLevelRASL-Function
            <GenSentenceFunc s.Joint>
            <GenResultFuncs s.OptResult>
            s.ScopeClass (e.Name) e.Sentences
          >;

        (#Function s.ScopeClass (e.Name) #NativeBody t.SrcPos e.Code) =
          (#CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code);

        (#Enum s.ScopeClass e.Name) =
          (#CmdEnum s.ScopeClass e.Name);

        (#Swap s.ScopeClass e.Name) =
          (#CmdSwap s.ScopeClass e.Name);

        (#Declaration s.ScopeClass e.Name) =
          (#CmdDeclaration s.ScopeClass e.Name);

        (#Ident e.Name) = (#CmdDefineIdent e.Name);

        (#NativeBlock t.SrcPos e.Code) =
          (#CmdEmitNativeCode t.SrcPos e.Code);
      }
      e.ProgramElements
    >;
}

HighLevelRASL-Function {
  s.FnGenFunction s.FnGenInitSubst s.FnGenSubst s.FnGenResult
  s.ScopeClass (e.Name) /* нет предложений */ =
    (#CmdEnum s.ScopeClass e.Name);

  s.FnGenFunction s.FnGenInitSubst s.FnGenSubst s.FnGenResult
  s.ScopeClass (e.Name) t.OneSentence =
    // Нет смысла вызывать оптимизацию совместного сопоставления
    // с образцом
    <HighLevelRASL-Function-Disjoint
      s.FnGenInitSubst s.FnGenSubst s.FnGenResult
      s.ScopeClass (e.Name) t.OneSentence
    >;

  s.FnGenFunction s.FnGenInitSubst s.FnGenSubst s.FnGenResult
  s.ScopeClass (e.Name) e.Sentences =
    <s.FnGenFunction
      s.FnGenInitSubst s.FnGenSubst s.FnGenResult
      s.ScopeClass (e.Name) e.Sentences
    >;
}

GenSentenceFunc {
  #Conjoint = HighLevelRASL-Function-Conjoint;
  #Disjoint = HighLevelRASL-Function-Disjoint;
}

GenResultFuncs {
  #NoOpt     = GenInitSubst-Simple GenSubst-Simple GenResult-Simple;
  #OptResult = GenInitSubst-Save   GenSubst-Save   GenResult-Opt;
}
